// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/02/ALU.hdl

/**
 * The ALU.  Computes a pre-defined set of functions out = f(x,y)
 * where x and y are two 16-bit inputs. The function f is selected 
 * by a set of 6 control bits denoted zx, nx, zy, ny, f, no.
 * The ALU operation can be described using the following pseudocode:
 *     if zx=1 set x = 0       // 16-bit zero constant
 *     if nx=1 set x = !x      // Bit-wise negation
 *     if zy=1 set y = 0       // 16-bit zero constant
 *     if ny=1 set y = !y      // Bit-wise negation
 *     if f=1  set out = x + y // Integer 2's complement addition
 *     else    set out = x & y // Bit-wise And
 *     if no=1 set out = !out  // Bit-wise negation
 *
 * In addition to computing out, the ALU computes two 1-bit outputs:
 *     if out=0 set zr = 1 else zr = 0 // 16-bit equality comparison
 *     if out<0 set ng = 1 else ng = 0 // 2's complement comparison
 */

CHIP ALU {

    IN  // 16-bit inputs:
        x[16], y[16],
        // Control bits:
        zx, // Zero the x input
        nx, // Negate the x input
        zy, // Zero the y input
        ny, // Negate the y input
        f,  // Function code: 1 for add, 0 for and
        no; // Negate the out output

    OUT // 16-bit output
        out[16],

        // ALU output flags
        zr, // 1 if out=0, 0 otherwise
        ng; // 1 if out<0, 0 otherwise

    PARTS:
	// Handles zx and nx
	Not(in=zx, out=notzx);
	UpConvert16(in=notzx, out=notzx16);
    And16(a=x, b=notzx16, out=andx);
	Not16(in=andx, out=notx);
	Mux16(a=andx, b=notx, sel=nx, out=muxx);

	// Handles zy and ny
	Not(in=zy, out=notzy);
	UpConvert16(in=notzy, out=notzy16);
	And16(a=y, b=notzy16, out=andy);			
	Not16(in=andy, out=noty);
	Mux16(a=andy, b=noty, sel=ny, out=muxy);
	
	// Handles f
	Add16(a=muxx, b=muxy, out=xplusy);
	And16(a=muxx, b=muxy, out=xandy);
	Mux16(a=xandy, b=xplusy, sel=f, out=muxxy);
	
	// Handles no
	Not16(in=muxxy, out=notmuxxy);
	Mux16(a=muxxy, b=notmuxxy, sel=no, out=out, out[0..7]=out1, out[8..15]=out2, out[15]=ng);
	
	// Handles zr and n
	Or8Way(in=out1, out=bottomout);
	Or8Way(in=out2, out=topout);
	Or(a=bottomout, b=topout, out=orout);
	Not(in=orout, out=zr);
}
